plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
}
// Load from local.properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}
def apiKey = localProperties.getProperty("IRONSOURCE_API_KEY")

android {
    namespace 'com.redwater.appmonitor'
    compileSdk 34

    defaultConfig {
        applicationId "com.redwater.appmonitor"
        minSdk 24
        targetSdk 34
        versionCode 4
        versionName "4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }


        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            ndk{
                debugSymbolLevel 'FULL'
            }
            buildConfigField "String", "IRON_SOURCE_APP_KEY", "\"${apiKey}\""
        }
        debug {
            buildConfigField "String", "IRON_SOURCE_APP_KEY", "\"${apiKey}\""
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    ksp {
        arg(new RoomSchemaArgProvider(new File(projectDir, "schemas")))
    }

    signingConfigs{
        release{
            storeFile file("../keystore.jks")
            storePassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEYSTORE_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    //implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation "androidx.core:core-splashscreen:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"


    //room
    def room_version = "2.5.2"
    implementation "androidx.room:room-runtime:$room_version"
    //for coroutnies
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // To use Kotlin annotation processing tool (ksp)
    ksp "androidx.room:room-compiler:$room_version"
    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    //work manager
    def work_version = "2.9.0"
    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$work_version"
    // optional - Test helpers
    androidTestImplementation "androidx.work:work-testing:$work_version"

    //navigation
    def nav_version = "2.5.2"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:33.1.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-messaging'

    //Utility
    implementation 'com.github.bumptech.glide:compose:1.0.0-alpha.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.patrykandpatrick.vico:views:1.8.0'
    implementation 'com.patrykandpatrick.vico:compose-m3:1.8.0'

    //ironSource
    implementation 'com.unity3d.ads-mediation:mediation-sdk:8.9.1'
    // ironSource Ad Quality SDK
    implementation 'com.unity3d.ads-mediation:adquality-sdk:7.25.1'
    implementation 'com.facebook.infer.annotation:infer-annotation:0.18.0'
    // Add Facebook Network
    implementation 'com.unity3d.ads-mediation:facebook-adapter:4.3.50'
    implementation 'com.facebook.android:audience-network-sdk:6.20.0'

    //Google play services
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.gms:play-services-appset:16.0.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.google.android.gms:play-services-basement:18.1.0'

    //CleverTap
    implementation 'com.clevertap.android:clevertap-android-sdk:7.4.0'
    implementation 'com.google.android.play:review:2.0.2'



    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:3.0-alpha-1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    File schemaDir

    RoomSchemaArgProvider(File schemaDir) {
        this.schemaDir = schemaDir
    }

    @Override
    Iterable<String> asArguments() {
        // Note: If you're using KAPT and javac, change the line below to
        // return ["-Aroom.schemaLocation=${schemaDir.path}".toString()].
        return ["room.schemaLocation=${schemaDir.path}".toString()]
    }
}
